prototype(Jonnitto.TextWithPlyr:Tag) < prototype(Neos.Fusion:Value) {
    config = ${Configuration.setting('Jonnitto.TextWithPlyr')}
    alignment = ${q(node).property('alignment')}
    align = ${this.alignment ? this.alignment : this.config.defaultAlignment}
    hasRow = ${this.align == 'left' || this.align == 'right' ? true : false}
    nodeType = ${String.toLowerCase(String.pregReplace(q(node).property('_nodeType.name'), '/[[:^alnum:]]/', '-'))}

    media = Jonnitto.Plyr:Tag {
        editable = false
    }

    @context.props = ${this}

    value = Neos.Fusion:Tag {
        @context {
            media = Neos.Fusion:Tag {
                attributes.class = ${props.hasRow ? props.config.class.colMedia : props.config.class.alignment[props.align]}
                content = ${props.media}
            }

            text = Neos.Fusion:Tag {
                @context.property = ${'text'}
                attributes.class = ${props.hasRow ? props.config.class.colText : false}
                content = ${q(node).property(property)}
                @process {
                    convertUris = Neos.Neos:ConvertUris
                    contentElementEditable = Neos.Neos:ContentElementEditable
                }
            }
        }

        attributes {
            class = Neos.Fusion:RawArray {
                nodeType = ${props.nodeType}
                container = ${props.config.container}
                align = ${'-' + String.toLowerCase(props.align)}
                container = ${props.hasRow ? props.config.class.hasRow : props.config.class.hasFloat}
            }
        }

        content = Neos.Fusion:Case {
            hasRow {
                condition = ${props.hasRow}
                renderer = Neos.Fusion:Tag {
                    attributes.class = Neos.Fusion:RawArray {
                        row = ${props.config.class.row}
                        align = ${props.config.class.alignment[props.align]}
                    }
                    content = ${text + media}
                }
            }
            hasFloat {
                condition = true
                renderer = ${media + text}
            }
        }
    }

    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }

    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
